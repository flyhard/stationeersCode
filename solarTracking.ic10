# ====== Register Aliases ======
alias returnAddr r10        # Return address storage
alias isNightState r11     # Night state flag (0/1)
alias sunVertical r12      # Sun vertical angle
alias sunHorizontal r13    # Sun horizontal angle
alias dialValue r14        # Dial input value
alias deviceHash r15       # Device hash checking
alias angleAdjust r16      # Temporary angle calculations

# ====== Constants ======
define SolarHorizontalAngle 0
define EveningMorningAngle 85  # 90 = night, 85 = twilight

# ====== Prefab Hashes ======
define SolarPanel HASH("StructureSolarPanel")
define SolarPanelDual HASH("StructureSolarPanelDual")
define SolarPanelReinforced HASH("StructureSolarPanelReinforced")
define SolarPanelDualReinforced HASH("StructureSolarPanelDualReinforced")
define wirelessBattery HASH("Battery_Wireless_cell")
define memory HASH("StructureLogicMemory")
define hDaySensor HASH("StructureDaylightSensor")

# ====== Device Aliases ======
alias DaySensor d0
alias Dial d1 # to adjust the horizontal angle
alias NSignal d2

# ====== Main Loop ======
fMain:
	jal fCalcVertical
	jal fCalcHorizontal
	jal fNightSignal
	yield
j fMain

# ====== Calculate Horizontal Rotation ======
fCalcHorizontal:
	move returnAddr ra
	jal fDialSettings
	pop dialValue
	
	brltz dialValue skipHorizontal
	mul angleAdjust 90 dialValue
	jr 2
	mul angleAdjust 90 SolarHorizontalAngle
	
	skipHorizontal:
	brdse DaySensor 3
	lb sunHorizontal hDaySensor Horizontal 0
	jr 2
	l sunHorizontal DaySensor Horizontal
	
	add sunHorizontal sunHorizontal angleAdjust
	breqz isNightState skipSetHorizontal
	add sunHorizontal angleAdjust 270
	
	skipSetHorizontal:
	sb SolarPanel Horizontal sunHorizontal
	sb SolarPanelDual Horizontal sunHorizontal
	sb SolarPanelReinforced Horizontal sunHorizontal
	sb SolarPanelDualReinforced Horizontal sunHorizontal
	move ra returnAddr
j ra

# ====== Calculate Vertical Tilt ======
fCalcVertical:
	brdse DaySensor 3
	lb sunVertical hDaySensor Vertical 0
	jr 2
	l sunVertical DaySensor Vertical
	
	sgt isNightState sunVertical EveningMorningAngle
	brgt sunVertical 95 isNight
	move isNightState 0
	jr 2
	move isNightState 1
	
	brgt sunVertical 75 adjustTwilight
	sub angleAdjust sunVertical 90
	abs angleAdjust angleAdjust
	jr 2
	move angleAdjust 15
	
	adjustTwilight:
	sb SolarPanel Vertical angleAdjust
	sb SolarPanelDual Vertical angleAdjust
	sb SolarPanelReinforced Vertical angleAdjust
	sb SolarPanelDualReinforced Vertical angleAdjust
j ra

isNight:
move isNightState 0
j ra

# ====== Get Dial Setting ======
fDialSettings:
	brdse Dial 3
	push -1
j ra

l deviceHash Dial PrefabHash
breq deviceHash wirelessBattery 3
push -1
j ra

s Dial Mode 3
l dialValue Dial Setting
push dialValue
j ra

# ====== Night Signal to Memory ======
fNightSignal:
	brdse NSignal 2
j ra

l deviceHash NSignal PrefabHash
brne deviceHash memory 3
s NSignal Setting isNightState
j ra
